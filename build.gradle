apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'

apply from: 'config/project-settings.gradle'

repositories {
	mavenLocal()
	maven {
		url 'https://repo1.maven.org/maven2/'
	}
	jcenter()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	repositories {
		maven {
			def releasesRepoUrl = "$buildDir/repos/releases"
			def snapshotsRepoUrl = "$buildDir/repos/snapshots"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

test {
	useJUnitPlatform()
}

tasks.withType(Test) {
	afterTest { desc, result ->
		println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
	}
}

dependencies {  
	implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.7'

    testImplementation("junit:junit:4.13")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.7.1")
}

jar {
	// configuration of MANIFEST.MF content
	manifest {
		attributes 'Specification-Title': 'Tigereyes DSL Framework - ' + project.name.capitalize(),
			'Specification-Version': project.version[0..2],
			'Specification-Vendor': 'Technische Universitaet Darmstadt, Germany',
			'Implementation-Title': "${project.group}.${project.name}",
			'Implementation-Version': project.version,
			'Implementation-Vendor': 'Technische Universitaet Darmstadt, Germany',
			'Build-Tool': 'Gradle-'+project.getGradle().getGradleVersion(),
			'Created-By': 'Groovy-'+GroovySystem.getVersion(),
			'Built-By': 'Tom Dinkelaker',
			'Build-Jdk': JavaVersion.current()
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'Tigereyes DSL Framework - ' + project.name.capitalize()
				description = 'TigersEye helps to build domain-specific languages (DSLs) as composable libraries'
				url = 'https://github.com/dinkelaker/tigerseye-core'
				/*properties = [
					myProp: "value",
					"prop.with.dots": "anotherValue"
				]*/
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.html'
					}
				}
				developers {
					developer {
						id = 'dinkelaker'
						name = 'Tom Dinkelaker'
						email = 'tom.dinkelaker@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git@github.com:dinkelaker/tigerseye-core.git'
					developerConnection = 'scm:git:ssh://github.com/tigerseye-core.git'
					url = 'https://github.com/dinkelaker/tigerseye-core'
				}
			}
		}
	}
}